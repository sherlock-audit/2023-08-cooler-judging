Rare Sangria Troll

high

# [H-02] No Access Control Modifier for rollLoan() can Force Borrower to Default and Lose Collateral
## Summary

Since there is no access control modifier verifying that the address who is calling `rollLoan()` is the borrower, this opens the door to a malicious lender forcing the borrower to default on his loan or repay a higher debt than originally intended.

## Vulnerability Detail

The [function](https://github.com/sherlock-audit/2023-08-cooler/blob/main/Cooler/src/Cooler.sol#L282) `provideNewTermsForRoll()` in `Cooler.sol` allows us to propose new terms for an existing `loanID`:

```javascript
function provideNewTermsForRoll(
        uint256 loanID_,
        uint256 interest_,
        uint256 loanToCollateral_,
        uint256 duration_
    ) external {
        Loan storage loan = loans[loanID_];

        if (msg.sender != loan.lender) revert OnlyApproved();

        loan.request =
            Request(
                loan.amount,
                interest_,
                loanToCollateral_,
                duration_,
                true
            );
    }
```

Where `loanID` specifices the index of the existing loan. Here we can propose a new interest rate, loanToCollateral & duration.

Examining scenario for an arbitrary Cooler generated with a popular stablecoin pair such as USDC/USDT:

We can propose a new higher interest-rate, and since there is no cap on it, this makes it even easier to force a borrower to default.

If we look at the `rollLoan()` [function](https://github.com/sherlock-audit/2023-08-cooler/blob/main/Cooler/src/Cooler.sol#L192) that a borrower is supposed to call to accept the new terms, we can see that there is no access modifier that restricts only the borrower to call it:
```javascript
function rollLoan(uint256 loanID_) external {
        Loan memory loan = loans[loanID_];

        if (block.timestamp > loan.expiry) revert Default();
        if (!loan.request.active) revert NotRollable();

        // Check whether rolling the loan requires pledging more collateral or not (if there was a previous repayment).
        uint256 newCollateral = newCollateralFor(loanID_);
        uint256 newDebt = interestFor(loan.amount, loan.request.interest, loan.request.duration);

        // Update memory accordingly.
        loan.amount += newDebt;
        loan.collateral += newCollateral;
        loan.expiry += loan.request.duration;
        loan.request.active = false;

        // Save updated loan info in storage.
        loans[loanID_] = loan;

        if (newCollateral > 0) {
            collateral().safeTransferFrom(msg.sender, address(this), newCollateral);
        }

        // If necessary, trigger lender callback.
        if (loan.callback) CoolerCallback(loan.lender).onRoll(loanID_, newDebt, newCollateral);
    }
```

What this allows is the malicious lender to `rollLoan()` on behalf of the borrower. He will transfer the new collateral needed, but he gets that back if the loan [defaults](https://github.com/sherlock-audit/2023-08-cooler/blob/main/Cooler/src/Cooler.sol#L318) anyway, so that is not an issue:
```javascript
function claimDefaulted(uint256 loanID_) external returns (uint256, uint256, uint256) {
        Loan memory loan = loans[loanID_];
        delete loans[loanID_];

        if (block.timestamp <= loan.expiry) revert NoDefault();

        // Transfer defaulted collateral to the lender.
        collateral().safeTransfer(loan.lender, loan.collateral);
```

The borrower is forced to either repay new debt (which he did not want to incur) or lose the collateral for his original loan, in both scenarios, the malicious lender wins.

Scenario A:

- Borrower repays additional needed debt, he is returned the additional collateral that is provided by the lender but overall is forced to pay a debt that he did intend to take out. What if the borrower cannot afford to repay the additional debt?

Scenario B:

- Borrower does not want to repay the additional debt, malicious lender collects collateral of original loan, as well as the one he transferred into the contract.

## Impact

Borrower is forced into a lose-lose situation, malicious lender is very easily allowed to execute this attack and is likely to do so, also leads to borrower always losing his collateral, thus classified this as High Severity.

## Code Snippet

[provideNewTermsForRoll()](https://github.com/sherlock-audit/2023-08-cooler/blob/main/Cooler/src/Cooler.sol#L282)
[rollLoan()](https://github.com/sherlock-audit/2023-08-cooler/blob/main/Cooler/src/Cooler.sol#L192)
[claimDefaulted](https://github.com/sherlock-audit/2023-08-cooler/blob/main/Cooler/src/Cooler.sol#L318)

## Tool used

VSCode, Manual Review

## Recommendation

Add an access control modifier to the `rollLoan()` function to only allow it to be called by the loan borrower.