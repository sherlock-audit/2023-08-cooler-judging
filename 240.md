Generous Glossy Poodle

high

# The tokens of the cooler contract is not checked when claiming a defaulted loan in Clearinghouse
## Summary

It is possible to claim a loan from a cooler contract that does not have gOHM as collateral which might result in accounting issues.

## Vulnerability Detail

When claiming a defaulted loan the cooler contract must have been created through the factory but is allowed to be any cooler contract with any tokens as collateral or debt.

```solidity
    function claimDefaulted(address[] calldata coolers_, uint256[] calldata loans_) external {
        uint256 loans = loans_.length;
        if (loans != coolers_.length) revert LengthDiscrepancy();

        uint256 totalDebt;
        uint256 totalInterest;
        uint256 totalCollateral;
        uint256 keeperRewards;
        for (uint256 i=0; i < loans;) {
            // Validate that cooler was deployed by the trusted factory.
            if (!factory.created(coolers_[i])) revert OnlyFromFactory();
            
            // Claim defaults and update cached metrics.
            (uint256 debt, uint256 collateral, uint256 elapsed) = Cooler(coolers_[i]).claimDefaulted(loans_[i]);
            uint256 interest = interestFromDebt(debt);
            unchecked {
                // Cannot overflow due to max supply limits for both tokens
                totalDebt += debt;
                totalInterest += interest;
                totalCollateral += collateral;
                // There will not exist more than 2**256 loans
                ++i;
            }

            // Cap rewards to 5% of the collateral to avoid OHM holder's dillution.
            uint256 maxAuctionReward = collateral * 5e16 / 1e18;
            // Cap rewards to avoid exorbitant amounts.
            uint256 maxReward = (maxAuctionReward < MAX_REWARD)
                ? maxAuctionReward
                : MAX_REWARD;
            // Calculate rewards based on the elapsed time since default.
            keeperRewards = (elapsed < 7 days)
                ? keeperRewards + maxReward * elapsed / 7 days
                : keeperRewards + maxReward;
        }

        // Decrement loan receivables.
        receivables = (receivables > totalDebt) ? receivables - totalDebt : 0;
        // Update outstanding debt owed to the Treasury upon default.
        uint256 outstandingDebt = TRSRY.reserveDebt(dai, address(this));
        // debt owed to TRSRY = user debt - user interest
        TRSRY.setDebt({
            debtor_: address(this),
            token_: dai,
            amount_: (outstandingDebt > (totalDebt - totalInterest))
                ? outstandingDebt - (totalDebt - totalInterest)
                : 0
        });

        // Reward keeper.
        gOHM.transfer(msg.sender, keeperRewards);
        // Unstake and burn the collateral of the defaulted loans.
        gOHM.approve(address(staking), totalCollateral - keeperRewards);
        MINTR.burnOhm(address(this), staking.unstake(address(this), totalCollateral - keeperRewards, false, false));
    }
```

Any attacker can create a cooler contract with worthless tokens, request and clear a loan with any collateral and debt values and default on the loan so that the debt and collateral values returned by cooler.claimDefaulted is controlled by the attacker. The totalDebt, totalInterest and totalCollateral could overflow since they are in an unchecked block. totalDebt could be set to a large number to set receivables to 0 and also set the treaury's debt to 0 while the collateral amounts could be 0 or just a few wei so that the gOHM transfers at the end of claimDefaulted do not fail.

## Impact

In the case that an attacker sets the treasury's reserveDebt to 0 a lot of the treasury's functionality might break. For example, in [OlympusTreasury.sol](https://github.com/OlympusDAO/olympus-v3/blob/19236eb1c02464df8fb79c7b59b7195d7511b338/src/modules/TRSRY/OlympusTreasury.sol) repaying debt would fail due to an [undeflow](https://github.com/OlympusDAO/olympus-v3/blob/19236eb1c02464df8fb79c7b59b7195d7511b338/src/modules/TRSRY/OlympusTreasury.sol#L142).

Additionally, since an attacker would have control of totalCollateral value they could claim a valid defaulted loan made through the Clearinghouse to receive keeperRewards then also claim a loan with worthless tokens to overflow totalCollateral to equal keeper rewards + 1. The gOHM tokens from the valid claim would not be burnt at the end of `claimDefaulted`.

## Code Snippet

[Clearinghouse.sol#L204](https://github.com/sherlock-audit/2023-08-cooler/blob/main/Cooler/src/Clearinghouse.sol#L204)

[Cooler.sol#L318-L333](https://github.com/sherlock-audit/2023-08-cooler/blob/main/Cooler/src/Cooler.sol#L318-L333)

## Tool used

Manual Review

## Recommendation

Consider checking that the cooler contract used to claim in `claimDefaulted` has gOHM as collateral.