Delightful Metal Stallion

high

# Incorrect debt repayment accounting when claiming defaulted loan
## Summary

The vulnerability involves incorrect debt repayment accounting when claiming defaulted loan, which can lead to the inflation of the outstanding debt owed to Olympus Treasury, causing a mis-accounting of the entire ecosystem.

## Vulnerability Detail

```solidity
// Findings are labeled with '<= FOUND'
// File: Cooler/src/Clearinghouse.sol
191:    function claimDefaulted(address[] calldata coolers_, uint256[] calldata loans_) external {
192:        ...
204:            (uint256 debt, uint256 collateral, uint256 elapsed) = Cooler(coolers_[i]).claimDefaulted(loans_[i]);
205:            uint256 interest = interestFromDebt(debt); // <= FOUND: incorrect debt repayment accouting on defaults claim
206:            unchecked {
207:                // Cannot overflow due to max supply limits for both tokens
208:                totalDebt += debt;
209:                totalInterest += interest;
210:                ...
230:        uint256 outstandingDebt = TRSRY.reserveDebt(dai, address(this));
231:        // debt owed to TRSRY = user debt - user interest
232:        TRSRY.setDebt({
233:            debtor_: address(this),
234:            token_: dai,
235:            amount_: (outstandingDebt > (totalDebt - totalInterest))
236:                ? outstandingDebt - (totalDebt - totalInterest) // <= FOUND
237:                : 0
238:        });
245:    }
// File: Cooler/src/Cooler.sol
318:    function claimDefaulted(uint256 loanID_) external returns (uint256, uint256, uint256) {
319:        ...
331:        if (loan.callback) CoolerCallback(loan.lender).onDefault(loanID_, loan.amount, loan.collateral);
332:        return (loan.amount, loan.collateral, block.timestamp - loan.expiry); // <= FOUND
333:    }
```

Referencing the above snippet, we can described the vulnerability in details:
1. When a user claims the defaults, an amount of `debt-owed-to-TRSRY` is calculated represented by the value of `totalDebt - totalInterest` (Clearinghouse#L236). This later would be subtracted by the `outstandingDebt` to account for the debt repayment. 
2. However, the contract calculates the unpaid `totalInterest` wrong based on the current `debt` amount. This `debt` is equivalent to the current `loan.amount` (Cooler.sol#L332) that already includes the unpaid interest itself (loan.amount = initial borrowed amount + unpaid interest), resulting in a larger value of `interest` (Clearinghouse#L205), hence larger `totalInterest`.
3. This mis-calculation leads to a **smaller** value of `debt-owed-to-TRSRY`. Ultimately `TRSRY.setDebt` is called to update the treasury accounting with a **larger** outstanding debt compared to the correct one.
   
## Impact

The impact of this vulnerability is significant as it can inflate the outstanding debt owned to Olympus Treasury. This mis-accounting can disrupt the entire ecosystem's financial calculations and affect the accuracy of debt records.

A POC is provided to confirm the inflated debts:

POC:

```solidity
function test_claimDefaulted_inflatedReserveDebt() public {
        // Check init conditions
        uint256 initReceivables = clearinghouse.receivables();// @audit-issue M-wrongDebtAccountedOnDefaultsClaim
        assertEq(initReceivables, 0, "receivables not 0");
        uint256 initDebt = TRSRY.reserveDebt(dai, address(clearinghouse));
        assertEq(initDebt, clearinghouse.FUND_AMOUNT(),  "Init reserveDebt");

        (uint256 borrow1, uint256 borrow2) = (clearinghouse.FUND_AMOUNT() / 3, clearinghouse.FUND_AMOUNT() * 2 / 3);
        uint256 totalBorrowed = borrow1 + borrow2;

        uint256[] memory ids = new uint256[](2);
        address[] memory coolers = new address[](2);
        {
            (Cooler cooler1, uint256 gohmNeeded1, uint256 loanID1) = _createLoanForUser(borrow1);
            (Cooler cooler2, uint256 gohmNeeded2, uint256 loanID2) = _createLoanForUser(borrow2);
            cooler1.getLoan(loanID1);
            cooler2.getLoan(loanID2);
            ids[0] = loanID1;
            ids[1] = loanID2;
            coolers[0] = address(cooler1);
            coolers[1] = address(cooler2);

            // Simulate unstaking outcome after defaults
            ohm.mint(address(clearinghouse), gohmNeeded1 + gohmNeeded2);
        }

        // Move forward after both loans have ended
        _skip(clearinghouse.fundTime() + clearinghouse.DURATION());

        uint256 debtAfterLent = TRSRY.reserveDebt(dai, address(clearinghouse));
        assertEq(debtAfterLent, initDebt + totalBorrowed);

        {
            deal(address(gohm), others, 0);
            // Claim defaulted loans
            vm.prank(others);
            clearinghouse.claimDefaulted(coolers, ids);

            assertEq(
                clearinghouse.receivables(),
                0,
                "receivables not cleared"
            );
            uint256 debtAfterDefaultsAccounted = TRSRY.reserveDebt(dai, address(clearinghouse));
            assertEq(
                initDebt,
                debtAfterDefaultsAccounted,
                "reserveDebt inflated"
            );
        }
    }
```

Result

> Running 1 test for src/test/Clearinghouse.t.sol:ClearinghouseTest
> [FAIL. Reason: Assertion failed.] test_claimDefaulted_inflatedReserveDebt() (gas: 1427569)
> Logs:
> Error: reserveDebt inflated
> Error: a == b not satisfied [uint]
> Left: 18000000000000000000000000
> Right: 18000049453556014261559746
> Test result: FAILED. 0 passed; 1 failed; 0 skipped; finished in 4.90ms
> Ran 1 test suites: 0 tests passed, 1 failed, 0 skipped (1 total tests)

The test result shows that if a `totalBorrowed` of `FUND_AMOUNT` (18 millions $DAI) went defaulted, after being claimed by the keeper, the outstanding `debtAfterDefaultsAccounted` owed to the Olympic Treasury grows 49.45 $DAI larger compared to `initDebt` (before borrowing).

## Code Snippet

https://github.com/sherlock-audit/2023-08-cooler/blob/main/Cooler/src/Clearinghouse.sol#L205
https://github.com/sherlock-audit/2023-08-cooler/blob/main/Cooler/src/Clearinghouse.sol#L236

## Tool used
Foundry

## Recommendation

To mitigate this vulnerability, it is recommended to calculate the debt to be repaid based on the interest derived from the initial borrowed amount `loan.request.amount` rather than the current `loan.amount`.

```solidity
function claimDefaulted(address[] calldata coolers_, uint256[] calldata loans_) external {
...
-    uint256 interest = interestFromDebt(debt);
+    Loan memory loan = loans[loans_[i]];
+    uint256 interest = interestFromDebt(loan.request.amount);
}
```