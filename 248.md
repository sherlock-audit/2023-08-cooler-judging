Generous Juniper Mink

high

# If `claimDefaulted` is called before `claimRepaid` all of `loan.unclaimed` will be lost
## Summary
Calling `claimDefaulted` before `claimRepaid` will delete the loan struct and will lose all of `loan.unclaimed`.

## Vulnerability Detail
If a loan has `repayDirect == false`, upon repaying the funds are sent to the contract and stored to `loans[loanID_].unclaimed` The user then has to claim them by calling `claimRepaid` 
```solidity
    function claimRepaid(uint256 loanID_) external {
        Loan memory loan = loans[loanID_];

        // Update storage.
        uint256 claim = loan.unclaimed;
        delete loans[loanID_].unclaimed;

        // Transfer repaid debt back to the lender.
        debt().safeTransfer(loan.lender, claim);
    }
```
However, there comes a problem if the loan is defaulted and the lender calls `claimDefaulted` before `claimRepaid`.
```solidity
   function claimDefaulted(uint256 loanID_) external returns (uint256, uint256, uint256) {
        Loan memory loan = loans[loanID_];
        delete loans[loanID_];  // @audit - deletes the loan struct

        if (block.timestamp <= loan.expiry) revert NoDefault();

        // Transfer defaulted collateral to the lender.
        collateral().safeTransfer(loan.lender, loan.collateral);

        // Log the event.
        factory().newEvent(loanID_, CoolerFactory.Events.DefaultLoan, 0);

        // If necessary, trigger lender callback.
        if (loan.callback) CoolerCallback(loan.lender).onDefault(loanID_, loan.amount, loan.collateral);
        return (loan.amount, loan.collateral, block.timestamp - loan.expiry);
    }
```
Upon calling `claimDefaulted` it deletes the loan struct. When doing so it also deletes the `loan.unclaimed` value. Meaning if there had been any unclaimed value in the loan, after calling `claimDefaulted` it is set to 0 and is forever lost.
This is especially a problem as `claimDefaulted` has no access control and anyone call call it and force the lender to suffer losses 

## Impact
Lender can lose all `unclaimed` value if loan goes into default.

## Code Snippet
https://github.com/sherlock-audit/2023-08-cooler/blob/main/Cooler/src/Cooler.sol#L318C2-L333C6
https://github.com/sherlock-audit/2023-08-cooler/blob/main/Cooler/src/Cooler.sol#L304C1-L313C6

## Tool used

Manual Review

## Recommendation
Upon calling `claimDefaulted` also redeem the `unclaimed` amount
